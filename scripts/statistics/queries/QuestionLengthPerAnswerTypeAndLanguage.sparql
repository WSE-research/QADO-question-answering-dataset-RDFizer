PREFIX qab: <urn:qa:benchmark#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>

SELECT 
    ?benchmark 
    ?lang 
    (COUNT(DISTINCT ?question) AS ?questions) 
    ?type 
    (MIN(?length) AS ?minLength) 
    (AVG(?length) AS ?avgLength) 
    (MAX(?length) AS ?maxLength)
{
    ?question a ?questionType ;
        qab:isElementOf ?benchmark ;
        qab:hasQuestion|qab:questionText ?questionText ;
        qab:hasAnswer ?answer .
    ?answer qab:answerText|qab:hasAnswerBody|(qab:hasAnswer/qab:answer) ?answerBody .
    ?questionType rdfs:subClassOf qab:Question .
    BIND(STRLEN(?questionText) AS ?length)
    BIND(LANG(?questionText) AS ?lang)
    BIND(DATATYPE(?answerBody) AS ?type)
} 
GROUP BY ?benchmark ?lang ?type 
ORDER BY ?benchmark ?lang ?type
